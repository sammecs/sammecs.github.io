---
layout: post
title: Python with native Bluetooth in Nix
---
## Background

One chill evening during the semester, I was hanging out in my favorite student café and heard people talking about an [application](https://github.com/seemoo-lab/opendrop) that could make Apple's AirDrop work on Linux. I was tinkering around with it, and found out that to use AirDrop, you need to send a BLE "ping" (advertisement) to the Apple device you want to communicate with. This "ping" should activate the AWDL interface. The author of the library [recommended a library](https://github.com/seemoo-lab/opendrop/issues/30) which could send these pings, so I set out to get this library to work: [apple-bleee](https://github.com/hexway/apple_bleee).

## Python with bluetooth

One of my first attempts to build a python interpreter capable of running apple-bleee was this:
```nix
# in file tinker.nix
with import <nixos-unstable> {};

rec {
  py = python3Packages;
  pybluezble = py.buildPythonPackage rec { ... };
  fleep = py.buildPythonPackage rec { ... };
  ctypescrypto = py.buildPythonPackage rec { ... };
  pygattlib = py.buildPythonPackage rec { ... };
  bleeePython = python3.withPackages (pp: with pp; [
    urllib3
    requests
    beautifulsoup4
    prettytable
    pillow
    pycrypto
    netifaces
    libarchive-c
    fleep
    ctypescrypto
    pybluezble
  ]);
}
```
If you now were to run `nix-build tinker.nix -A bleeePython` and `./result/bin/python3 apple-bleee/ble_read_state.py`, you would get this error:
```python
Traceback (most recent call last):
  File "apple_bleee/ble_read_state.py", line 65, in <module>
    toggle_device(dev_id, True)
  File "/home/sammecs/Documents/apple-bleee/apple_bleee/utils/bluetooth_utils.py", line 88, in toggle_device
    hci_sock = socket.socket(socket.AF_BLUETOOTH,
AttributeError: module 'socket' has no attribute 'AF_BLUETOOTH'
```
Python was able to import the module `socket`, but it did not find the attribute `AF_BLUETOOTH`. It turns out the module `socket` is part of the python standard library. From the documentation:

> Depending on the system and the build options, various socket families are supported by this module.

So the python interpreter itself was built without bluetooth support. Easy enough: Giving python access to a bluetooth library (e.g. bluez) during build time will make it support bluetooth. Here's my attempt:
```nix
  bluePython = python3.overrideAttrs (old: {
    buildInputs = old.buildInputs ++ [ bluez ];
  });
```
I simply added `bluez` to the `buildInputs` of python. In the `bluePython` REPL, `from socket import AF_BLUETOOTH` works!

Now we just need to stick the python with bluetooth support and the necessary packages together:
```nix
  bleeePython = bluePython.withPackages (pp: with pp; [ ... ]);
```
Problem solved, right?

No.
```python
>>> from socket import AF_BLUETOOTH
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
ImportError: cannot import name 'AF_BLUETOOTH' from 'socket' (/nix/store/ja04f3cmapzb3f2mvjrb883bfqclsirq-python3-3.7.6/lib/python3.7/socket.py)
```
So, the same error as before (it just looks a bit different because I didn't run the program directly). But why? This is the result of a session of [ripgrep](https://github.com/BurntSushi/ripgrep)ing nixpkgs for definitions:
```nix
# in pkgs/development/interpreters/python/default.nix :
  passthruFun = { ..., self }: let
    pythonPackages = callPackage ../../../top-level/python-packages.nix {
      python = self;
      overrides = packageOverrides;
    };
    in rec {
      buildEnv = callPackage ./wrapper.nix { python = self; inherit (pythonPackages) requiredPythonModules; };
      withPackages = import ./with-packages.nix { inherit buildEnv pythonPackages;};
      pkgs = pythonPackages;
      # ... (and some other definitions)
    };

# I try to find out where passthruFun is used and how
# ... later in default.nix
  python37 = callPackage ./cpython {
    self = python37;
    sourceVersion = {
      major = "3";
      minor = "7";
      patch = "6";
      suffix = "";
    };
    sha256 = "0gskry19ylw91p38pdq36qcgk6h3x5i4ia0ik977kw2943kwr8jm";
    inherit (darwin) configd;
    inherit passthruFun;
  };

# in pkgs/development/interpreters/python/cpython/default.nix :
# this passthru is used for every python interpreter derivation
  passthru = passthruFun rec {
    inherit self sourceVersion packageOverrides;
    implementation = "cpython";
    libPrefix = "python${pythonVersion}";
    executable = libPrefix;
    pythonVersion = with sourceVersion; "${major}.${minor}";
    sitePackages = "lib/${libPrefix}/site-packages";
    inherit hasDistutilsCxxPatch pythonForBuild;
  };
```
So, python (the interpreter derivation) takes an argument `self` which points to whatever-`callPackage`-returns. When we override the `buildInputs` of python, we get a new derivation, but `self` is not updated. The function `overrideAttrs`, provided by `callPackage`, is not something fancy like a fixpoint: It is designed to override attributes of one derivation, without changing the stuff that relies on it. So, `self` is never changed when using `overrideAttrs`. But this was not a problem when running the interpreter without packages. The problem must be caused by something in `withPackages`.

You could look at the definitions of `withPackages` in `with-packages.nix` and `buildEnv` in `wrapper.nix` to find the source of the problem (I did that, and it was pretty interesting). But the fundamental problem can already be seen when looking at the "dependencies" of `withPackages` and `pythonPackages`: They only really depend on `self` (and `packageOverrides`, but that's not important here). But `self` was not overriden with `overrideAttrs`, so `withPackages` has no idea that the python interpreter was updated! Is it possible to override `self` to point to the correct derivation? Let's try it:
```nix
  bluePython = python3.overrideAttrs (old: {
    buildInputs = old.buildInputs ++ [ bluez.dev bluez ];
  });
  bluePython' = bluePython.override { self = bluePython'; };
  bleeePython = bluePython'.withPackages (pp: with pp; [ ... ]);
```
In the python REPL:
```python
>>> from socket import AF_BLUETOOTH # it works! :D
>>> import requests # this should work
>>> import fleep # this should work, but doesn't
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
ModuleNotFoundError: No module named 'fleep'
```

Very weird indeed. What happened? *Now* we need to look at the definition of `buildEnv` (remeber that `withPackages f` is shorthand for `buildEnv.override { extraLibs = f pythonPackages; }`):
```nix
# in pkgs/development/interpreters/python/wrapper.nix
{ stdenv, python, buildEnv, makeWrapper
, extraLibs ? []
, extraOutputsToInstall ? []
, postBuild ? ""
, ignoreCollisions ? false
, permitUserSite ? false
, requiredPythonModules
# Wrap executables with the given argument.
, makeWrapperArgs ? []
, }:

# Create a python executable that knows about additional packages.
let
  env = let
    paths = requiredPythonModules (extraLibs ++ [ python ] ) ;
    pythonPath = "${placeholder "out"}/${python.sitePackages}";
    pythonExecutable = "${placeholder "out"}/bin/${python.executable}";
  in buildEnv {
    name = "${python.name}-env";

    inherit paths;
    inherit ignoreCollisions;
    extraOutputsToInstall = [ "out" ] ++ extraOutputsToInstall;

    postBuild = ''
      . "${makeWrapper}/nix-support/setup-hook"

      if [ -L "$out/bin" ]; then
          unlink "$out/bin"
      fi
      mkdir -p "$out/bin"

      for path in ${stdenv.lib.concatStringsSep " " paths}; do
        if [ -d "$path/bin" ]; then
          cd "$path/bin"
          for prg in *; do
            if [ -f "$prg" ]; then
              rm -f "$out/bin/$prg"
              if [ -x "$prg" ]; then
                makeWrapper "$path/bin/$prg" "$out/bin/$prg" --set NIX_PYTHONEXECUTABLE ${pythonExecutable} --set NIX_PYTHONPATH ${pythonPath} ${if permitUserSite then "" else ''--set PYTHONNOUSERSITE "true"''} ${stdenv.lib.concatStringsSep " " makeWrapperArgs}
              fi
            fi
          done
        fi
      done
    '' + postBuild;

    inherit (python) meta;

    passthru = ...;
  };
in env
```
I don't understand all of the code, but that's OK. I know this: Some packages were not included in `bleeePython`, and they were given to `buildEnv` in the `extraLibs` parameter (and nowhere else). `extraLibs` only appears two times in `wrapper.nix`: In the parameter list and in this line:
```nix
    paths = requiredPythonModules (extraLibs ++ [ python ] ) ;
```
So what does `requiredPythonModules` do? Ripgrepping shows where `requiredPythonModules` is used:
```nix
# in pkgs/top-level/python-packages.nix
  # Check whether a derivation provides a Python module.
  hasPythonModule = drv: drv?pythonModule && drv.pythonModule == python;

  # Get list of required Python modules given a list of derivations.
  requiredPythonModules = drvs: let
    modules = filter hasPythonModule drvs;
  in unique ([python] ++ modules ++ concatLists (catAttrs "requiredPythonModules" modules));
```
What this does is:
1. It filters out any derivation which does not satisfy `hasPythonModule`.
2. It uses the attribute `requiredPythonModules` of all supplied derivations to build a list of python modules.
3. It applies some logistics (sorting out duplicates, concatenating lists and so on).

Does `fleep` satisfy `hasPythonModule`? Let's find out in the `nix repl`:
```nix
nix-repl> :l tinker.nix # here I define bleeePython and such
Added 8 variables.

# this works because each python interpreter ships its own package set
# in the pkgs attribute, which is defined in passthruFun
nix-repl> bluePython'.pkgs.hasPythonModule fleep
false

nix-repl> bluePython'
«derivation /nix/store/w9bhg8ph0akygg55x6s6axcq0dfrbwpp-python3-3.7.6.drv»

nix-repl> fleep.pythonModule
«derivation /nix/store/hmnprsc6nkxhjh4da7gdrpffi9ygcpbh-python3-3.7.6.drv»
```
Indeed: `fleep` does not fulfill `hasPythonModule`, because the python interpreters for `fleep` and `bluePython'` differ. At first, I used `toPythonModule` to solve this problem:
```nix
# in pkgs/top-level/python-packages.nix
  # Convert derivation to a Python module.
  toPythonModule = drv:
    drv.overrideAttrs( oldAttrs: {
      # Use passthru in order to prevent rebuilds when possible.
      passthru = (oldAttrs.passthru or {})// {
        pythonModule = python;
        pythonPath = [ ]; # Deprecated, for compatibility.
        requiredPythonModules = requiredPythonModules drv.propagatedBuildInputs;
      };
    });
```
`fleep` already has the attributes `pythonModule` and `requiredPythonModules`, but `toPythonModule` overrides them again, setting `pythonModule` to the correct interpreter.
```nix
# in tinker.nix
  bleeePython = bluePython'.withPackages (pp: with pp; [
    urllib3
    requests
    beautifulsoup4
    prettytable
    pillow
    pycrypto
    netifaces
    libarchive-c
  ] ++ (map bluePython'.pkgs.toPythonModule [
    fleep
    ctypescrypto
    pybluezble
  ]));
```

## (My) Final solution

But really, this problem can be solved more elegantly by changing the interpreter my custom packages are built with. In the moment, my custom packages are built with the normal python interpreter without bluetooth, but used by the interpreter with bluetooth support. Changing `py` (which is used to select `buildPythonPackage`) from `python3Packages` to `bluePython'.pkgs` will build all packages with `bluePython'` (yes, this does take a long time to build, because even libraries in nixpkgs (like `requests`) will need to be rebuilt with the new interpreter). Using that, `withPackages` works as expected!
```nix
  py = bluePython'.pkgs;
  fleep = py.buildPythonPackge rec { ... };
  bleeePython = bluePython'.withPackages (pp: with pp; [ ... ]);
```

My full code for `bleeePython` can be found at [GitHub](https://gist.github.com/sammecs/1d8964a587173786bdfc4acb355b02d9).

## A more hacky Solution (draft)

The python interpreter with bluetooth support does not differ so much from the python interpreter without bluetooth support. There *could* be some problems with shared library linking, but maybe we're lucky: To reduce the build time, I want to use the prebuilt python packages from nixpkgs, and use the `toPythonModule` hack to glue together the packages and the interpreter:
```nix
  py = python3Packages;
  bleeePython = bluePython'.withPackages (pp: with python3Packages;
    (map bluePython'.pkgs.toPythonModule [
      urllib3
      requests
      beautifulsoup4
      prettytable
      pillow
      pycrypto
      netifaces
      libarchive-c
      fleep
      ctypescrypto
      pybluezble
    ]));
```
Python REPL:
```python
>>> from socket import AF_BLUETOOTH
>>> import fleep
>>> import requests
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
  File "/nix/store/lkzy3013bcbp025rbczbfkhax9gk5c7i-python3-3.7.6-env/lib/python3.7/site-packages/requests/__init__.py", line 44, in <module>
    import chardet
ModuleNotFoundError: No module named 'chardet'
```
Ah yes, `requests` depends on `chardet`, but `chardet` has not been given to the bleeePython environment. This is because `toPythonModule` sets `requiredPythonModules` to `requiredPythonModules drv.propagatedBuildInputs`. We know that `requiredPythonModules` filters out modules which were not built against the same python interpreter, and `chardet`s `pythonModule` attribute is not the same as `bluePython'`. Maybe there is something like a recursive `toPythonModule`? I dunno, maybe I will write an update.
