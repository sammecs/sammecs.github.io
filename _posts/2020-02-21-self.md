---
layout: post
title: Why do Python interpreters in Nixpkgs have an attribute "self"?
---
Once upon a time, I wanted to build a python interpreter with bluetooth support to run a [set of applications](https://github.com/hexway/apple_bleee). This was (roughly) my first attempt:
```nix
with import <nixos-unstable> {};

rec {
  py = python36Packages;
  pybluezble = py.buildPythonPackage rec { ... };
  fleep = py.buildPythonPackage rec { ... };
  ctypescrypto = py.buildPythonPackage rec { ... };
  bleeePython = python36.buildEnv.override {
    extraLibs = with py; [
      urllib3
      requests
      beautifulsoup4
      prettytable
      pillow
      pycrypto
      netifaces
      libarchive-c
      fleep
      ctypescrypto
      pybluezble
    ];
  };
}
```
For the Nix newbies:
1. `import <nixos-unstable>` is a Nix expression. Nix knows where to find it using the `NIX_PATH` environment variable (see [this Nix Pill](https://nixos.org/nixos/nix-pills/nix-search-paths.html)). For me, `echo $NIX_PATH` returns
```
nixpkgs=/nix/var/nix/profiles/per-user/root/channels/nixos:nixos-config=/etc/nixos/configuration.nix:/nix/var/nix/profiles/per-user/root/channels
```
and I indeed find a folder titled `nixos-unstable` at `/nix/var/nix/profiles/per-user/root/channels`, with a `default.nix` inside. This `default.nix` is what is being evaluated when calling `import <nixos-unstable>`.
2. `import <nixos-unstable>` is a function. I call this function with one argument, `{}`. This returns an attribute set (nixpkgs).
3. `with import <nixos-unstable> {};` exposes all the attributes in nixpkgs to the current namespace. Instead of writing `(import <nixos-unstable> {}).python3`, I can simply type `python3` now.
4. `rec` before an attribute set allows recursive definitions. For `{ py = python36Packages; my-package = py.buildPythonPackage { ... }; }` to work, I need `rec` (`python36Packages` is exposed in nixpkgs, `py` is not).
5. `python36.buildEnv.override { extraLibs = with py; [ ... ]; }` is exactly the same as `python36.withPackages (pp: with pp; [ ... ])`. `withPackages` is defined using `buildEnv.override { extraLibs = ...; }`.
6. `...` stands for code I omitted. It's not valid Nix code (at least not outside of an argument list).

I found two problems with this approach:
1. Python did not find the packages I wrote myself (`import ctypescrypto` threw an error).
2. Python was not built with native bluetooth support: `from socket import AF_BLUETOOTH` threw an error (`AF_BLUETOOTH` is a symbol [exported by the standard library](https://docs.python.org/3.6/library/socket.html) since Python 3.3).

## How to build Python with custom packages

I was confused. Wasn't `python36.buildEnv` supposed to build a Python environment with the given Python packages? What could have possibly failed? So I looked into the Nix source code: What does `python36.buildEnv` actually *do*?

*This section is about nixpkgs. Most paths should be interpreted with respect to the [nixpkgs root](https://github.com/nixos/nixpkgs).*

You can find out that `python36` is defined in `pkgs/development/interpreters/python/default.nix`, for example by [ripgrep](https://github.com/BurntSushi/ripgrep)ping for `python36` in nixpkgs. This could either directly lead you to the right file, or you could try the more "canonical" approach:
1. Locate `python36` in `pkgs/top-level/all-packages.nix`:
```nix
pythonInterpreters = callPackage ./../development/interpreters/python {};
inherit (pythonInterpreters) python27 python35 python36 python37 python38 python39 python3Minimal pypy27 pypy36;
```
The second line is the one found by ripgrepping. It says "define the symbols `python27`, `python35`, ..., `pypy36` in the current attribute set like they are defined in the set `pythonInterpreters`". `pythonInterpreters` is defined in the line above -- it is generally helpful to show context (for example, `-C 3`) while ripgrepping!
2. Look into `pkgs/development/interpreters/python/default.nix`, and find the definition of `python36`.

This file only contains 216 lines, with a lot of repititions (not exactly boilerplate, but very similar code). This is the dfinition of `python36`:
```nix
  python36 = callPackage ./cpython {
    self = python36;
    sourceVersion = {
      major = "3";
      minor = "6";
      patch = "10";
      suffix = "";
    };
    sha256 = "1pj0mz1xl27khi250p29c0y99vxg662js8zp71aprkf8i8wkr0qa";
    inherit (darwin) configd;
    inherit passthruFun;
  };
```
There's no `buildEnv` attribute declared with `callPackage`. Where does it come from? Intuition says it must have something to do with `passthruFun`, and ripgrepping reveals that `passthruFun` is a python-only thing. It's just a function taking a set of attributes and returning a set of attributes. The attribute set it returns actually contains an attribute `buildEnv`!
```nix
passthruFun = { ... , packageOverrides, ..., self }:
  let pythonPackages = callPackage ../../../top-level/python-packages.nix {
      python = self;
      overrides = packageOverrides;
    };
  in rec {
    ...
    buildEnv = callPackage ./wrapper.nix { python = self; inherit (pythonPackages) requiredPythonModules; };
};
```
In 
Each Python interpreter calls `passthruFun` with their specific arguments to make their own `passthru` (read more about `passthru` in the [Nixpkgs manual](https://nixos.org/nixpkgs/manual/#ssec-stdenv-attributes)), so you don't need to write `python36.passthru.buildEnv`, and can just use `python36.buildEnv` instead.

On we go to find out what happens in `./wrapper.nix`. To fix my problem, I am only interested in what happens to `extraLibs`, the thing I try to override when using custom Python packages. `extraLibs` is used to define a new attribute `paths`:
```nix
paths = requiredPythonModules (extraLibs ++ [ python ]);
```
`requiredPythonModules`, `extraLibs` and `python` are all arguments to the `./wrapper.nix` function, and `requiredPythonModules` is part of the Python packages collection.

*To be continued...*
